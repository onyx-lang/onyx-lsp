
use core {package, eprintf, os}
use json_rpc {
    Method,
    Context,
    Status,
    Notification
}

InitializeRequest :: struct {
    capabilities: ClientCapabilities;

    rootPath: ? str;
    rootUri: ? str;
}

ClientCapabilities :: struct {
    textDocument: struct {
        publishDiagnostics: struct {
            relatedInformation: bool;
            tagSupport: struct {
                valueSet: [] i32;
            }
        }
    };
}

InitializeResult :: struct {
    capabilities: ServerCapabilities;

    serverInfo: struct {
        name: str;
        version: str;
    };
}

ServerCapabilities :: struct {
    positionEncoding: str;

    textDocumentSync: struct {
        openClose: bool;
        change: i32;
        save: bool;
    };

    referencesProvider: bool;

    definitionProvider: bool;

    hoverProvider: bool;
}

perform_initialization :: (root_path: str) {
    if !load_config_new(root_path) {
        if !load_config(root_path) {
            log(.Error, "Failed find 'lsp' section in 'onyx-pkg.kdl'. Also failed to open config file 'onyx-lsp.ini'. Using the defaults.\n");
            return
        }
    }

    logf(.Info, "Successfully loaded config from '{}'", root_path)
}

@Method.{"initialize"}
(c: &Context, input: &InitializeRequest, out: &InitializeResult) -> Status {
    root_path := do {
        if input.rootPath do return input.rootPath?
        if input.rootUri  do return input.rootUri? |> uri_decode()
        return "."
    }

    log(.Info, core.tprintf("Client has capabilties: {*p}", &input.capabilities));
    log(.Info, core.tprintf("Client is in folder: {}", root_path));

    perform_initialization(root_path);

    out.capabilities = .{
        positionEncoding = "utf-16",
        textDocumentSync = .{
            openClose = false,
            change = 0,
            save = true
        },
        definitionProvider = true,
        referencesProvider = true,
        hoverProvider = true
    };

    out.serverInfo = .{
        name = "onyx-lsp",
        version = "0.0.1"
    };

    return .Success;
}

@Notification.{"initialized"}
(c: &Context, _: &void) {
    log(.Info, "Successfully initialized");

    //
    // If this is a project, update all diagnostics and symbol information
    // right away, instead of waiting for a file to be saved.
    if global_config.lsp.mode == "project" {
        update_diagnostics(c, "");
    }
}
